# Â© 2024 Amazon Web Services, Inc. or its affiliates. All Rights Reserved.

# This AWS Content is provided subject to the terms of the AWS Customer Agreement
# available at http://aws.amazon.com/agreement or other written agreement between
# Customer and either Amazon Web Services, Inc. or Amazon Web Services EMEA SARL or both.

${autogen_warning}
data "aws_iam_policy_document" "gha" {
  statement {
    actions = ["sts:AssumeRoleWithWebIdentity"]

    principals {
      identifiers = ["arn:aws:iam::${account_id}:oidc-provider/token.actions.githubusercontent.com"]
      type        = "Federated"
    }

    condition {
      test     = "StringEquals"
      values   = ["sts.amazonaws.com"]
      variable = "token.actions.githubusercontent.com:aud"
    }

    condition {
      test     = "StringLike"
      variable = "token.actions.githubusercontent.com:sub"

      values = [
        "repo:${github_org_name}/${repo_name}:job_workflow_ref:${github_org_name}/${reusable_workflow_repo_name}/.github/workflows/terraform-workflow.yml@refs/heads/*",
        "repo:${github_org_name}/${repo_name}:job_workflow_ref:${github_org_name}/${reusable_workflow_repo_name}/.github/workflows/terraform-force-unlock.yml@refs/heads/*",
        %{ if env_name == "dev" }
        "repo:${github_org_name}/${repo_name}:job_workflow_ref:${github_org_name}/${reusable_workflow_repo_name}/.github/workflows/terraform-destroy.yml@refs/heads/*",
        %{ endif ~}
      ]
    }
  }
}

data "aws_iam_policy_document" "permissions_boundary" {
  # checkov:skip=CKV_AWS_49:This is a permissions boundary
  # checkov:skip=CKV_AWS_111:This is a permissions boundary
  # checkov:skip=CKV_AWS_109:This is a permissions boundary
  # checkov:skip=CKV_AWS_1:This is a permissions boundary
  # checkov:skip=CKV2_AWS_40:This is a permissions boundary

  statement {
    actions   = ["*"]
    resources = ["*"]
    effect    = "Allow"
    sid       = "AllowAll"
  }

  statement {
    sid       = "DenyIaCAssetChanges"
    actions   = ["*"]
    effect    = "Deny"
    resources = ["arn:aws:iam::*:role/${naming_prefix}-*"]
%{ if is_ci_test_account }
    condition {
      test     = "ArnNotEquals"
      variable = "aws:PrincipalArn"
      values   = ["arn:aws:iam::${account_id}:role/${repo_name}-terraform-module-ci-exec"]
    }
%{ endif ~}
  }

  statement {
    sid = "DenyChangesToBoundaryPolicy"

    actions = [
      "iam:CreatePolicy",
      "iam:CreatePolicyVersion",
      "iam:DeletePolicy",
      "iam:DeletePolicyVersion",
      "iam:SetDefaultPolicyVersion"
    ]

    effect    = "Deny"
    resources = ["arn:aws:iam::*:policy/${naming_prefix}-permissions-boundary-policy"]
  }

  statement {
    sid = "DenyChangesToKMSKeys"

    actions = [
      "kms:PutKeyPolicy",
      "kms:ReplicateKey"
    ]

    effect    = "Deny"
    resources = ["arn:aws:kms:*:*:key/*"]
  }

  statement {
    sid = "EnforceActionsHaveBoundary"

    actions = [
      "iam:AttachRolePolicy",
      "iam:CreateRole",
      "iam:DeleteRolePolicy",
      "iam:DetachRolePolicy",
      "iam:PutRolePermissionsBoundary",
      "iam:PutRolePolicy"
    ]

    effect    = "Deny"
    resources = ["*"]

    condition {
      test     = "StringNotLike"
      variable = "iam:PermissionsBoundary"
      values   = ["arn:aws:iam::*:policy/${naming_prefix}-permissions-boundary-policy"]
    }
  }
}

resource "aws_iam_policy" "permissions_boundary" {
  name   = "${naming_prefix}-permissions-boundary-policy"
  path   = "/"
  policy = data.aws_iam_policy_document.permissions_boundary.json
}

resource "aws_iam_role" "gha" {
  # checkov:skip=CKV_AWS_274:Allow AdministratorAccess usage here because it is appropriate for usage by TF + GHA

  assume_role_policy   = data.aws_iam_policy_document.gha.json
  max_session_duration = 7200
  name                 = "${naming_prefix}-${repo_name_mod}-${env_name}-gha-exec"
  path                 = "/"

  managed_policy_arns = [
    "arn:aws:iam::aws:policy/AdministratorAccess",

    # The following three policies are attached via AWS Config in an LZA Environment
    # We're therefore attaching them explicitly here to squash a perpetual diff
    "arn:aws:iam::aws:policy/AmazonSSMDirectoryServiceAccess",
    "arn:aws:iam::aws:policy/AmazonSSMManagedInstanceCore",
    "arn:aws:iam::aws:policy/CloudWatchAgentServerPolicy"

  ]

  # permissions_boundary = aws_iam_policy.${repo_name}_${env_name}_role_permission_boundary.arn

  tags = {
    Name = "${naming_prefix}-${repo_name_mod}-${env_name}-gha-exec"
  }
}


%{~ if is_ci_test_account && env_name == "dev" }
####################################################################################################
# Terraform Module CI Workflow Resources
####################################################################################################

data "aws_iam_policy_document" "terraform_module_ci" {
  statement {
    actions = ["sts:AssumeRoleWithWebIdentity"]

    principals {
      identifiers = ["arn:aws:iam::${account_id}:oidc-provider/token.actions.githubusercontent.com"]
      type        = "Federated"
    }

    condition {
      test     = "StringEquals"
      values   = ["sts.amazonaws.com"]
      variable = "token.actions.githubusercontent.com:aud"
    }

    condition {
      test     = "StringLike"
      values   = ["repo:${github_org_name}/terraform-aws-*:job_workflow_ref:${github_org_name}/${reusable_workflow_repo_name}/.github/workflows/terraform-module-ci.yaml@refs/heads/*"]
      variable = "token.actions.githubusercontent.com:sub"
    }
  }
}

resource "aws_iam_role" "terraform_module_ci" {
  # checkov:skip=CKV_AWS_274:Allow AdministratorAccess usage here because it is appropriate for usage by TF + GHA.

  assume_role_policy   = data.aws_iam_policy_document.terraform_module_ci.json
  managed_policy_arns  = ["arn:aws:iam::aws:policy/AdministratorAccess"]
  max_session_duration = 7200
  name                 = "${naming_prefix}-${repo_name_mod}-terraform-module-ci-exec"
  path                 = "/"

  tags = {
    Name = "${naming_prefix}-${repo_name_mod}-terraform-module-ci-exec"
  }
}
%{ endif ~}
